from Bio.SeqIO.FastaIO import SimpleFastaParser
import glob
import os
include: "Common.snake"
include: "Binning.snake"


BINS = read_selected_bins("bayespaths/selected_bins.txt") if os.path.exists("bayespaths/selected_bins.txt") \
                else read_selected_bins("subgraphs/bin_merged/selected_bins.txt")

rule prepare:
    input: "desman/contig_bins.done"

rule all:
    input: expand("desman/{Bin}/desman_analysis_done", Bin=BINS)

rule bin_contigs:
    input:
        bin = "binning/clustering_gt%d_merged.csv" % MIN_CONTIG_SIZE,
        fasta = "profile/assembly.fasta"
    output: expand("desman/Bin_{id}/Bin_{id}.fasta", id=BIN_IDS)
    params: " ".join(BIN_IDS)
    shell: """
  {SCRIPTS}/Split_fasta_by_bin.py {input.fasta} {input.bin} desman -l {params} -d
  """

rule contigs_bed:
    input: "desman/{bin}/{bin}.fasta"
    output: "desman/{bin}/{bin}.bed"
    shell: "{SCRIPTS}/WriteBed.py {input} > {output}"

rule bam_by_bin:
    input:
        global_bam = "profile/assembly/{sample}.sorted.bam",
        bin_bed = "desman/{bin}/{bin}.bed"
    output:
        bin_bam = "desman/{bin}/{sample}.{bin}.bam"
    threads: THREADS
    shell: """
    samtools view -b -L {input.bin_bed} {input.global_bam} -@{threads} > {output.bin_bam}
    """

rule bam_readcount:
    input:
        fasta = "desman/{bin}/{bin}.fasta",
        fai = "desman/{bin}/{bin}.fasta.fai",
        bam = "desman/{bin}/{sample}.{bin}.bam",
        bed = "desman/{bin}/{bin}.bed"
    output: "desman/{bin}/{sample}.cnt.gz"
    log:
        "desman/{bin}/bam_readcount_{sample}.log"
    shell: """
        samtools index {input.bam} {input.bam}.bai
        bam-readcount -l {input.bed} -f {input.fasta} {input.bam} 2> {log} | gzip > {output}
        """

rule assembly_bed:
    input:
        "annotation/assembly.gff",
    output:
        "desman/assembly.bed"
    shell: """
      {SCRIPTS}/Gff_to_bed.py {input} {output}
      """

rule scg_bed:
    input:
        all_bed = "desman/assembly.bed",
        scg_file = "annotation/SCG.fna"
    output:
        scg_bed = "desman/SCG.bed"
    run:
        seq_names = {name.split()[0] for name, seq in SimpleFastaParser(
            open(input.scg_file))}
        with open(output.scg_bed, 'w') as scg_bed:
            for line in open(input.all_bed):
                if line.split()[3] in seq_names:
                    scg_bed.write(line)

rule build_scg_coordinates_for_ExtractCountFreqGenes:
    input:
        scg_bed = "desman/SCG.bed",
        bin_scg = "subgraphs/bin_init/{bin}/SCG.fna"
    output:
        coordinates = "desman/{bin}/scg_coordinates.tsv"
    run:
        dict_scg = {header.split()[0]: header.split() for header, seq in SimpleFastaParser(open(input.bin_scg))}
    # NODE_11_length_160076_cov_189.092050_50 COG0552 strand=+
        with open(output.coordinates, "w") as handle :
            for line in open(input.scg_bed):
                contig, start, end, orf = line.rstrip().split("\t")
                if orf in dict_scg:
                    cog, strand = dict_scg[orf][1:]
                    cog, strand = dict_scg[orf][1:]
                    strand = str((strand == "strand=+")*1-(strand == "strand=-")*1)
                    contig = "_".join(orf.split("_")[:-1])
                    handle.write(",".join([cog, contig, start, end, orf, strand])+"\n")

rule extract_counts:
    input:
        expand("{{path}}/{sample}.cnt.gz", sample=SAMPLES),
        scg_coordinates = "{path}/scg_coordinates.tsv"
    output:
        "{path}/count.csv"
    params:
        input_dir = "{path}"
    log:
        "{path}/extract_counts.log"
    shell: """
    python {DSCRIPTS}/ExtractCountFreqGenes.py {input.scg_coordinates} {params.input_dir} --output_file {output} &>> {log}
        """

checkpoint filter_variant:
    input:
        "desman/{bin}/count.csv"
    output:
        expand("desman/{{bin}}/freqs_{output_file_type}",
               output_file_type=["sel_var.csv", "p_df.csv", "q_df.csv", "r_df.csv", "tran_df.csv", "log.txt"])
    params: "desman/{bin}/freqs_"
    log:
        "desman/Logs/{bin}_Filter_variant.log"
    shell: """
        Variant_Filter.py {input} -o {params} -p -m {MIN_COV_DESMAN} 2>{log}
        """

rule run_desman:
    input:
        sel_var = "desman/{bin}/freqs_sel_var.csv",
        err = "desman/{bin}/freqs_tran_df.csv"
    output:
        "desman/{bin}/Run_{g}_{r}/fit.txt"
    log:
        "desman/Logs/Run_{g}_{r}/{bin}_{g}_{r}.log"
    shell: """
      desman {input.sel_var} -e {input.err} -o $(dirname {output}) -m {MIN_COV_DESMAN} -i 100 -g {wildcards.g} -s {wildcards.r} &> {log}
      """


rule desman_fit:
    input: expand("desman/{{bin}}/Run_{g}_{r}/fit.txt", g=range(1, DESMAN_HAPLOTYPE_NB+1), r=range(DESMAN_REPEAT))
    output: "desman/{bin}/Deviance.csv"
    shell: """
        cat {input} | cut -d"," -f2- > {output}
        sed -i '1iH,G,LP,Dev' {output}
    """

rule desman_plot:
    input: "{path}/Deviance.csv",
    output: "{path}/Deviance.pdf"
    log: "{path}/PlotDev.log"
    shell: """
        {DSCRIPTS}/PlotDev.R -l {input} -o {output} 2> {log}
    """

rule haplo_seqs:
    input:
        fasta = "profile/assembly.fasta",
        deviance_file = "{path}/Deviance.pdf",
        scg_coordinates = "{path}/scg_coordinates.tsv"
    output: "{path}/best_run.txt"
    params: "{path}"
    shell: """
        python {DSCRIPTS}/resolvenhap.py {params}/Run > {params}/best_run.txt
        
        cut -d"," -f 1 < {params}/freqs_sel_var.csv | sort | uniq | sed '1d' > {params}/coregenes.txt

        mkdir -p {params}/haplotype_seqs
        python {DSCRIPTS}/GetVariantsCore.py {input.fasta} {input.scg_coordinates} $(cut -d"," -f5 {params}/best_run.txt) {params}/coregenes.txt -o {params}/haplotype_seqs/
    """

def do_we_need_to_run_desman(wildcards):
    freq_sel_var_file = checkpoints.filter_variant.get(bin=wildcards.bin).output[0]
    # "binning/{bin}/count.csv"
    nb_line = len([inline for inline in open(freq_sel_var_file)])
    if nb_line > 1:
        return "desman/" + wildcards.bin + "/best_run.txt"
    else:
        return freq_sel_var_file


rule is_desman_done:
    input: do_we_need_to_run_desman
    output: touch("desman/{bin}/desman_analysis_done")
