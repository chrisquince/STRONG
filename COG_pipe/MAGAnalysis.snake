include: "Common.snake"

rule all: 
  input: "mag_analysis/AlignAll/AlignAllR_assign.tsv"

rule aggregate_SCG:
    input: "subgraphs/bin_init/folder_done"
    output: expand("mag_analysis/SCGs/{cog}.fna", cog=ANNOTATED_COGS) 
    shell: """
     {SCRIPTS}/ExtractSCGs.py $(dirname {input}) {COG_FILE} mag_analysis/
      """

rule extract_ref_SCGs:
    input: SCG_DATA + "/All_COGs.tar.gz"
    output: expand("mag_analysis/All_{cog}.ffn", cog=ANNOTATED_COGS)
    shell: "tar -xf {input} -C mag_analysis"

rule mafft:
    input: scg = "{path}/SCGs/{cog}.fna",
           database_scg = "{path}/All_{cog}.ffn"
    output: all_scg = "{path}/AlignAll/{cog}_all.ffn",
            alignement = "{path}/AlignAll/{cog}_all.gffn"
    log: "{path}/AlignAll/mafft_{cog}.log"
    threads: 1000
    shell:"""
    cat {input.scg} {input.database_scg} > {output.all_scg}
    mafft --thread {threads} {output.all_scg} > {output.alignement} 2>{log}
    """

rule trimal :
    input: "{path}/{cog}_all.gffn"
    output: "{path}/{cog}_al.gfa"
    shell: "trimal -in {input} -out {output} -gt 0.9 -cons 60"

rule generate_data_for_fastree:
    input: trimed = expand("{{path}}/{cog}_al.gfa", cog = ANNOTATED_COGS),
           alignement = expand("{{path}}/{cog}_all.gffn", cog = ANNOTATED_COGS)
    output: gfa = "{path}/AlignAllR.gfa",
            names = "{path}/Names.txt",
            temp = temp("{path}/temp_combine_genes.tmp")
    shell:"""
    cat {input.alignement} | grep ">" | sed 's/_COG.*//' | sort | uniq | sed 's/>//g' > {output.names}
    {SCRIPTS}/CombineGenes.pl {output.names} {input.trimed} > {output.temp}
    {SCRIPTS}/MapTI.pl {SCG_DATA}/TaxaSpeciesR.txt < {output.temp} > {output.gfa}
    """

rule fasttree:
    input: "{path}/AlignAllR.gfa"
    output: "{path}/AlignAllR.tree"
    log: "{path}/SelectR.out"
    shell:"""
    FastTreeMP -nt -gtr < {input} 2> {log} > {output}
    """

rule  Assign_tree:
    input: "{path}/AlignAllR.tree"
    output: "{path}/AlignAllR_assign.tsv"
    shell :"""
    {SCRIPTS}/AssignTree.py {input} {SCG_DATA}/TaxaSpeciesR.txt {SCG_DATA}/all_taxa_lineage.tsv > {output}
    """
